pipeline {
    agent any
    parameters {
        choice(name: 'NODE_LOCATION', choices: ['local', 'aws'], description: 'Choose where to deploy')
        booleanParam(name: 'DEPLOY', defaultValue: false, description: 'Enable deployment')
        choice(name: 'DEPLOY_TYPE', choices: ['docker', 'kubernetes'], description: 'Select deployment type')
    }
    environment {
        IMAGE_TAG = "latest"  // Will be updated from build job
        KUBECONFIG = "${env.WORKSPACE}/kubeconfig"
        BUILD_STATUS = 'NOT_DEPLOYED'
    }
    stages {
        stage('Checkout') {
            steps {
                checkout scm
            }
        }
        stage('Setup Environment') {
            when {
                expression { params.DEPLOY }
            }
            steps {
                script {
                    // Load .env from Jenkins credentials
                    withCredentials([file(credentialsId: 'third_eye_env_file', variable: 'ENV_FILE')]) {
                        // Remove existing .env file if it exists (handles read-only files)
                        sh 'rm -f .env || true'
                        
                        def envContent = readFile(file: env.ENV_FILE)
                        writeFile(file: '.env', text: envContent)
                        
                        // Parse and set environment variables
                        envContent.split('\n').each { line ->
                            if (line && !line.startsWith('#') && line.contains('=')) {
                                def parts = line.split('=', 2)
                                if (parts.length == 2) {
                                    def key = parts[0].trim()
                                    def value = parts[1].trim()
                                    env."${key}" = value
                                }
                            }
                        }
                        
                        // Set deployment-specific variables
                        env.BUILD_PROJECT_NAME = "${env.ENV_PROJECT_NAME}_build"
                        env.PROJECT_NAME_WITH_DASH = env.ENV_PROJECT_NAME.replace('_', '-')
                        env.REGISTRY = env.DOCKER_REGISTRY
                        env.REPOSITORY = "${env.DOCKER_REPOSITORY}/${env.DOCKER_IMAGE_NAME}"
                        env.NGINX_CONF = "/etc/nginx/sites-available/${env.PROJECT_NAME_WITH_DASH}"
                    }
                }
            }
        }
        stage('Check & Create Nginx Configuration') {
            when {
                expression { params.DEPLOY && params.NODE_LOCATION == 'local' }
            }
            steps {
                script {
                    // Check if the Nginx configuration file exists
                    def configExists = sh(script: "test -f ${env.NGINX_CONF} && echo 'exists' || echo 'not exists'", returnStdout: true).trim()

                    if (configExists == 'not exists') {
                        echo "Nginx configuration file does not exist. Creating it now..."

                        // Create Nginx configuration
                        sh "sudo cat nginx.conf | sudo tee ${env.NGINX_CONF} > /dev/null"

                        // Replace placeholders in the configuration file
                        sh "sudo sed -i 's|SERVER_NAME|${env.SERVER_NAME}|g' ${env.NGINX_CONF}"
                        sh "sudo sed -i 's|DOCKER_PORT|${env.DOCKER_PORT}|g' ${env.NGINX_CONF}"

                        echo "Nginx configuration file created."

                        // Ensure Nginx is aware of the new configuration
                        sh "sudo ln -sf ${env.NGINX_CONF} /etc/nginx/sites-enabled/"
                    } else {
                        echo "Nginx configuration file already exists."
                    }
                }
            }
        }
        stage('Retrieve Image Tag from Build Job') {
            when {
                expression { params.DEPLOY }
            }
            steps {
                script {
                    echo "Retrieving image tag from ${env.BUILD_PROJECT_NAME}"

                    // Construct the API URL for the latest successful build
                    def api_url = "https://${env.JENKINS_DOMAIN}/job/${env.BUILD_PROJECT_NAME}/lastSuccessfulBuild/api/json"

                    echo "Hitting API URL: ${api_url}"
                    
                    withCredentials([usernamePassword(credentialsId: 'jenkins-admin-credentials', usernameVariable: 'JENKINS_USER', passwordVariable: 'JENKINS_TOKEN')]) {
                        // Execute the curl command to retrieve the JSON response
                        def buildInfoJson = sh(script: "curl -u ${JENKINS_USER}:${JENKINS_TOKEN} ${api_url}", returnStdout: true).trim()

                        // Log the raw JSON response for debugging
                        echo "Raw JSON response: ${buildInfoJson}"

                        // Extract the build number using grep
                        def imageTag = sh(script: """
                            echo '${buildInfoJson}' | grep -o '"id":"[0-9]*"' | head -1 | tr -dc '0-9'
                        """, returnStdout: true).trim()

                        echo "Retrieved image tag (build number): ${imageTag}"

                        // Update IMAGE_TAG environment variable
                        env.IMAGE_TAG = imageTag

                        if (env.REGISTRY && env.REPOSITORY && imageTag) {
                            if (params.DEPLOY_TYPE == 'kubernetes') {
                                // Replace the placeholder in the deployment YAML
                                sh "sed -i 's|:latest|:${imageTag}|g' ${WORKSPACE}/deployment.yaml"
                            }
                            
                            if (params.DEPLOY_TYPE == 'docker') {
                                // Update docker-compose.yml with correct image tag
                                sh """
                                sed -i 's|image: .*|image: ${env.REGISTRY}/${env.REPOSITORY}:${imageTag}|' docker-compose.yml
                                """
                            }
                        } else {
                            echo "One or more required variables (REGISTRY, REPOSITORY, imageTag) are not defined. Skipping image update."
                        }
                    }
                }
            }
        }
        stage('Deploy to Docker (Local)') {
            when {
                allOf {
                    expression { params.DEPLOY }
                    expression { params.NODE_LOCATION == 'local' }
                    expression { params.DEPLOY_TYPE == 'docker' }
                }
            }
            steps {
                script {
                    echo "Deploying with Docker on local node..."

                    sh 'docker compose down || true'
                    sh 'docker compose pull'
                    sh 'docker compose up -d'

                    echo "Waiting for container to be healthy..."
                    sleep 30

                    def containerRunning = sh(script: "docker ps -q -f name=${env.ENV_PROJECT_NAME}", returnStdout: true).trim()
                    if (!containerRunning) {
                        error "Container ${env.ENV_PROJECT_NAME} is not running"
                    } else {
                        echo "Container ${env.ENV_PROJECT_NAME} is running"
                        
                        // Health check before updating Nginx
                        sh """
                            HTTP_STATUS=\$(curl -s -o /dev/null -w "%{http_code}" -H "Host: ${env.SERVER_NAME}" http://localhost:${env.DOCKER_PORT})
                            echo "HTTP Status: \$HTTP_STATUS"
                            
                            if [ "\$HTTP_STATUS" -eq 200 ] || [ "\$HTTP_STATUS" -eq 302 ]; then
                                sudo sed -i 's|proxy_pass .*;|proxy_pass http://0.0.0.0:${env.DOCKER_PORT};|' ${env.NGINX_CONF}
                                sudo nginx -s reload
                                echo 'Nginx configuration updated and reloaded successfully.'
                                
                                # Scale down Kubernetes deployment if running
                                replicas=\$(kubectl get deployment ${env.PROJECT_NAME_WITH_DASH}-app -o=jsonpath='{.spec.replicas}' 2>/dev/null) || true
                                if [ "\$replicas" != "" ] && [ \$replicas -gt 0 ]; then
                                    kubectl scale deployment ${env.PROJECT_NAME_WITH_DASH}-app --replicas=0
                                    echo 'Kubernetes deployment scaled down successfully.'
                                fi
                            else
                                echo 'Service not available. Nginx configuration not updated.'
                                exit 1
                            fi
                        """
                        env.BUILD_STATUS = 'DEPLOYED'
                    }
                }
            }
        }
        stage('Deploy to Kubernetes (Local)') {
            when {
                allOf {
                    expression { params.DEPLOY }
                    expression { params.NODE_LOCATION == 'local' }
                    expression { params.DEPLOY_TYPE == 'kubernetes' }
                }
            }
            steps {
                script {
                    echo "Deploying to Kubernetes on local node..."

                    // Verify Kubernetes configuration
                    sh 'kubectl cluster-info'
                    
                    // Delete existing secret if it exists
                    sh "kubectl delete secret ${env.PROJECT_NAME_WITH_DASH}-secret || true"

                    // Deploy to Kubernetes
                    sh """
                    kubectl create secret generic ${env.PROJECT_NAME_WITH_DASH}-secret --from-env-file=${WORKSPACE}/.env
                    kubectl apply -f ${WORKSPACE}/service.yaml
                    kubectl apply -f ${WORKSPACE}/deployment.yaml
                    """
                    
                    echo "Waiting for deployment to be ready..."
                    sleep 60

                    // Check deployment status
                    sh "kubectl describe deployment/${env.PROJECT_NAME_WITH_DASH}-app"
                    
                    // Verify service and get NodePort
                    def nodePort = sh(script: "kubectl get service ${env.PROJECT_NAME_WITH_DASH}-service -o=jsonpath='{.spec.ports[0].nodePort}'", returnStdout: true).trim()
                    echo "Service NodePort: ${nodePort}"

                    // Get cluster IP address
                    def clusterIP = sh(script: "kubectl get nodes -o=jsonpath='{.items[0].status.addresses[0].address}'", returnStdout: true).trim()
                    echo "Cluster IP: ${clusterIP}"

                    // Verify service accessibility and update Nginx
                    sh """
                        HTTP_STATUS=\$(curl -s -o /dev/null -w "%{http_code}" -L http://${clusterIP}:${nodePort})
                        echo "HTTP Status: \$HTTP_STATUS"
                        
                        if [ "\$HTTP_STATUS" -eq 200 ] || [ "\$HTTP_STATUS" -eq 302 ]; then
                            echo "Service is reachable at http://${clusterIP}:${nodePort}"

                            echo "Updating Nginx configuration at ${env.NGINX_CONF}..."
                            sudo sed -i 's|proxy_pass .*;|proxy_pass http://${clusterIP}:${nodePort};|' ${env.NGINX_CONF}
                            
                            echo "Reloading Nginx..."
                            sudo nginx -s reload
                            
                            echo "Nginx reloaded successfully"
                            
                            # Remove Docker container if running
                            DOCKER_CONTAINER=\$(docker ps -q -f name=${env.ENV_PROJECT_NAME})
                            if [ "\$DOCKER_CONTAINER" ]; then
                                echo "Docker container ${env.ENV_PROJECT_NAME} is running. Removing it..."
                                docker rm -f ${env.ENV_PROJECT_NAME}
                            else
                                echo "Docker container ${env.ENV_PROJECT_NAME} is not running. Skipping removal"
                            fi
                        else
                            echo "Service is not reachable at http://${clusterIP}:${nodePort}. HTTP Status: \$HTTP_STATUS"
                            exit 1
                        fi
                    """
                    env.BUILD_STATUS = 'DEPLOYED'
                }
            }
        }
        stage('Sentry Release') {
            when {
                expression { params.DEPLOY && env.BUILD_STATUS == 'DEPLOYED' }
            }
            steps {
                script {
                    withCredentials([string(credentialsId: 'sentry-auth-token', variable: 'SENTRY_AUTH_TOKEN')]) {
                        sh """
                        VERSION=\$(git rev-parse HEAD)
                        sentry-cli releases -o ${env.SENTRY_ORG} -p ${env.SENTRY_PROJECT} new \$VERSION || true
                        sentry-cli releases -o ${env.SENTRY_ORG} -p ${env.SENTRY_PROJECT} set-commits --auto \$VERSION || true
                        sentry-cli releases -o ${env.SENTRY_ORG} -p ${env.SENTRY_PROJECT} deploys \$VERSION new -e ${env.SENTRY_ENVIRONMENT} || true
                        sentry-cli releases -o ${env.SENTRY_ORG} -p ${env.SENTRY_PROJECT} finalize \$VERSION || true
                        """
                    }
                }
            }
        }
    }
    post {
        success {
            script {
                if (env.BUILD_STATUS == 'DEPLOYED') {
                    withCredentials([
                        string(credentialsId: 'mailjet-api-key', variable: 'MAIL_JET_API_KEY'),
                        string(credentialsId: 'mailjet-api-secret', variable: 'MAIL_JET_API_SECRET')
                    ]) {
                        sh """curl -s \
                        -X POST \
                        --user \$MAIL_JET_API_KEY:\$MAIL_JET_API_SECRET \
                        https://api.mailjet.com/v3.1/send \
                        -H "Content-Type:application/json" \
                        -d '{
                            "Messages":[
                                    {
                                            "From": {
                                                    "Email": "${env.MAIL_JET_EMAIL_ADDRESS}",
                                                    "Name": "Jenkins Notification"
                                            },
                                            "To": [
                                                    {
                                                            "Email": "${env.MY_EMAIL_ADDRESS}",
                                                            "Name": "Development Team"
                                                    }
                                            ],
                                            "Subject": "Jenkins Build Pipeline your project ${currentBuild.fullDisplayName} Ran Successfully",
                                            "TextPart": "Hola Development Team, your project ${currentBuild.fullDisplayName} is now deployed",
                                            "HTMLPart": "<h3>Hola Development Team, your project ${currentBuild.fullDisplayName} is now deployed </h3> <br> <p> Build Url: ${env.BUILD_URL}  </p>"
                                    }
                            ]
                        }' || true"""
                    }

                    // Trigger common_readme job
                    def commitMessage = sh(script: "git log -1 --pretty=%B", returnStdout: true).trim()
                    if (!commitMessage.contains("Automatic Update")) {
                        def expandedProjectUrl = "https://github.com/arpansahu/${env.ENV_PROJECT_NAME}"
                        build job: 'common_readme', parameters: [
                            string(name: 'project_git_url', value: expandedProjectUrl),
                            string(name: 'environment', value: 'prod')
                        ], wait: false
                    }
                }
            }
        }
        failure {
            script {
                withCredentials([
                    string(credentialsId: 'mailjet-api-key', variable: 'MAIL_JET_API_KEY'),
                    string(credentialsId: 'mailjet-api-secret', variable: 'MAIL_JET_API_SECRET')
                ]) {
                    sh """curl -s \
                    -X POST \
                    --user \$MAIL_JET_API_KEY:\$MAIL_JET_API_SECRET \
                    https://api.mailjet.com/v3.1/send \
                    -H "Content-Type:application/json" \
                    -d '{
                        "Messages":[
                                {
                                        "From": {
                                                "Email": "${env.MAIL_JET_EMAIL_ADDRESS}",
                                                "Name": "Jenkins Notification"
                                        },
                                        "To": [
                                                {
                                                        "Email": "${env.MY_EMAIL_ADDRESS}",
                                                        "Name": "Developer Team"
                                                }
                                        ],
                                    "Subject": "Jenkins Build Pipeline your project ${currentBuild.fullDisplayName} Failed",
                                    "TextPart": "Hola Development Team, your project ${currentBuild.fullDisplayName} deployment failed",
                                    "HTMLPart": "<h3>Hola Development Team, your project ${currentBuild.fullDisplayName} is not deployed, Build Failed </h3> <br> <p> Build Url: ${env.BUILD_URL}  </p>"
                                }
                        ]
                    }' || true"""
                }
            }
        }
        always {
            // Cleanup
            sh 'rm -f .env || true'
        }
    }
}
